import java.time.format.DateTimeFormatter

plugins {
    id "io.github.gradle-nexus.publish-plugin"  version "$nexusPublishPluginVersion"
    id 'com.github.ben-manes.versions'          version "$versionsPluginVersion"
    id 'org.ajoberstar.grgit'                   version "$grgitVersion"
    id 'org.jreleaser'                          version "$jreleaserVersion" apply false
    id "eclipse"
    id "idea"
    id 'org.jetbrains.kotlin.jvm'               version "$kotlinVersion" apply false
    id 'io.kotest'                              version "$kotestVersion" apply false
    id 'java-platform'
}


def buildTimeAndDate = grgit.head().dateTime

ext {
    isTravis                 = System.getenv("TRAVIS") == "true"
    isTravisPullRequest      = System.getenv("TRAVIS_PULL_REQUEST") != "false"
    pullRequestId            = System.getenv("TRAVIS_PULL_REQUEST")
    pullRequestBranch        = System.getenv("TRAVIS_PULL_REQUEST_BRANCH")
    hasGithub                = System.getenv("GITHUBTOKEN") && System.getenv("GITHUBREPO")
    hasSonar                 = System.getenv("SONARORG") && System.getenv("SONARLOGIN")
    sonarDefaultURL          = "https://sonarcloud.io"
    sonarDefaultProjectKey   = "org.jbake:jbake-base:jbake-core"
    sonarURL                 = System.getenv("SONARHOST") ?: sonarDefaultURL
    sonarProjectKey          = System.getenv("SONARPROJECTKEY") ?: sonarDefaultProjectKey
    buildDate                = buildTimeAndDate.format(DateTimeFormatter.ofPattern('yyyy-MM-dd'))
    buildTime                = buildTimeAndDate.format(DateTimeFormatter.ofPattern('HH:mm:ss.SSSZ'))
    isReleaseVersion         = !version.endsWith("SNAPSHOT")
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]?${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

subprojects {
    //dependencies {
        //constraints {
    configurations.configureEach {
        resolutionStrategy {
            // OrientDB
            force "org.graalvm.truffle:truffle-api:$graalVmVersion"

            // Pug4j
            force "org.graalvm.sdk:graal-sdk:$graalVmVersion"
            //runtime "org.graalvm.js:js:$graalVmVersion"
            //runtime "org.graalvm.regex:regex:$graalVmVersion"
            force "org.graalvm.js:js-scriptengine:$graalVmVersion"
            //runtime "org.graalvm.tools:profiler:$graalVmVersion"
            //runtime "org.graalvm.tools:chromeinspector:$graalVmVersion"
        }
    }
}
