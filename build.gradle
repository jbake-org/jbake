plugins {
    id "eclipse"
    id "idea"
    id "io.sdkman.vendors"              version "1.2.1"     apply false
    id "com.jfrog.bintray"              version "1.8.4"     apply false
    id "com.github.kt3k.coveralls"      version "2.8.2"     apply false
    id "org.sonarqube"                  version "2.7"       apply false
    id 'com.github.ben-manes.versions'  version '0.21.0'
    id "nebula.optional-base"           version "5.0.2"     apply false
}

// common variables
ext {
    asciidoctorjVersion = '1.6.2'
    asciidoctorjDiagramVersion = '1.5.16'
    commonsIoVersion = '2.6'
    commonsConfigurationVersion = '1.10'
    commonsLangVersion = '3.8.1'
    commonsVfs2Version = '2.3'
    args4jVersion = '2.33'
    freemarkerVersion = '2.3.28'
    junit4Version = '4.12'
    junit5Version = '5.4.1'
    junitPioneer = '0.3.0'
    jgitVersion = '5.3.0.201903130848-r'
    flexmarkVersion = '0.40.34'
    jettyServerVersion = '9.4.15.v20190215'
    orientDbVersion = '3.0.18'
    groovyVersion = '2.5.6'
    slf4jVersion = '1.7.26'
    logbackVersion = '1.2.3'
    assertjCoreVersion = '2.9.1'
    thymeleafVersion = '3.0.11.RELEASE'
    jsonSimpleVersion = '1.1.1'
    jade4jVersion = '1.2.7'
    mockitoVersion = '2.26.0'
    jsoupVersion = '1.11.3'
    pebbleVersion = '3.0.8'

    isTravis = (System.getenv("TRAVIS") == "true")
    isTravisPullRequest = (System.getenv("TRAVIS_PULL_REQUEST")) != "false"
    pullRequestId = System.getenv("TRAVIS_PULL_REQUEST")
    hasGithub = System.getenv("GITHUBTOKEN") && System.getenv("GITHUBREPO")
    hasSonar = System.getenv("SONARORG") && System.getenv("SONARLOGIN")
    sonarDefaultURL = "https://sonarcloud.io"
    sonarDefaultProjectKey = "org.jbake:jbake-base:jbake-core"
    sonarURL = System.getenv("SONARHOST") ?: sonarDefaultURL
    sonarProjectKey = System.getenv("SONARPROJECTKEY") ?: sonarDefaultProjectKey
}

/**
 * Apply coveralls to the root project as we just need it here to send the
 * aggregated coverage execution data from the jacocoRootReport task
 */
apply plugin: 'com.github.kt3k.coveralls'

/**
 * Apply jacoco plugin to all projects and add jcenter as default repository
 */
allprojects {
    apply plugin: 'jacoco'

    if ( JavaVersion.current().isJava8Compatible() ) {

        apply plugin: 'checkstyle'

        tasks.withType(Checkstyle) {
            reports {
                xml.enabled false
                html.enabled true
            }
        }

    }

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = jacocoVersion
    }

}

/**
 * Common setup for all subprojects
 */
subprojects {

    apply plugin: 'java'
    apply plugin: 'nebula.optional-base'

    apply from: "$rootDir/gradle/signing.gradle"
    // We do not publish any jars from the jbake-dist project
    if ( project.name != "jbake-dist" ) {
      apply from: "$rootDir/gradle/maven-publishing.gradle"
    }

    // add source and target compatibility for all JavaCompile tasks
    tasks.withType(JavaCompile) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

        /**
         *  Fix for warning: [options] bootstrap class path not set in conjunction with -source
         *  See https://stackoverflow.com/questions/42599422/warning-options-bootstrap-class-path-not-set-in-conjunction-with-source-1-7
         */
        File file = file("${System.properties.getProperty("java.home")}/lib/rt.jar")
        if ( file.exists() ) {
            options.setBootstrapClasspath(files(file))
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        compile "ch.qos.logback:logback-classic:$logbackVersion", optional
        compile "ch.qos.logback:logback-core:$logbackVersion", optional

        testCompile("org.junit.jupiter:junit-jupiter-api:$junit5Version")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit5Version")
        testCompile("org.junit-pioneer:junit-pioneer:$junitPioneer")
        // compatibility for Junit 4 test
        testCompile "junit:junit:$junit4Version"
        testRuntime("org.junit.vintage:junit-vintage-engine:$junit5Version")

        testCompile "org.assertj:assertj-core:$assertjCoreVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]?${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    //set jvm for all Test tasks (like test and smokeTest)
    tasks.withType(Test) {

        def args = ['-Xms512m', '-Xmx3g', '-Dorientdb.installCustomFormatter=false=false','-Djna.nosys=true']

        /**
         * jdk9 build is unable to determine the amount of MaxDirectMemorySize
         * See https://pastebin.com/ECvQeHx0
         */
        if ( JavaVersion.current().java9Compatible ) {
            args << '-XX:MaxDirectMemorySize=2g'
        }
        jvmArgs args
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    jacocoTestReport.dependsOn test
}

task jacocoMerge(type: JacocoMerge) {
    description 'Merge all testreport execution data from subprojects excluding jbake-dist'
    dependsOn subprojects.test
    executionData subprojects.findAll{it.name!="jbake-dist"}.jacocoTestReport.executionData
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn jacocoMerge

    sourceDirectories.from files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from files(subprojects.sourceSets.main.output)
    executionData.from jacocoMerge.executionData

    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task testReport(type: TestReport) {
    description "Generate an aggregated Testreport for all projects"

    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoRootReport
    // Skip Task if not run on CI Server
    onlyIf { System.env.'CI' }
}
