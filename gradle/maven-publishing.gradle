apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = "jbake"
                description = project.description
                url = project.website
                developers {
                    developer {
                        id = 'jonbullock'
                        name = 'Jonathan Bullock'
                        email = 'jonbullock@gmail.com'
                        url = 'http://jonathanbullock.com'
                        timezone = 'Europe/London'
                    }
                    developer {
                        id = 'ancho'
                        name = 'Frank Becker'
                        email = 'frank@calmdevelopment.de'
                        timezone = 'Europe/Berlin'
                    }
                    developer {
                        id = 'manikmagar'
                        name = 'Manik Magar'
                        url = 'https://manik.magar.me/'
                        timezone = 'America/New_York'
                    }
                    developer {
                        id = 'aldrinleal'
                        name = 'Aldrin Leal'
                        timezone = 'America/Bogota'
                    }
                }
                scm {
                    url = project.vcs
                    connection = 'scm:git:git@github.com:jbake-org/jbake.git'
                    developerConnection = 'scm:git:https://github.com/jbake-org/jbake.git'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = project.issues
                }
                mailingLists {

                    mailingList {
                        name = 'jbake-dev'
                        subscribe = 'jbake-dev@googlegroups.com'
                        unsubscribe = 'jbake-dev+unsubscribe@googlegroups.com'
                        archive = 'http://groups.google.com/group/jbake-dev'
                    }
                    mailingList {
                        name = 'jbake-user'
                        subscribe = 'jbake-user@googlegroups.com'
                        unsubscribe = 'jbake-user+unsubscribe@googlegroups.com'
                        archive = 'http://groups.google.com/group/jbake-user'
                    }
                }
            }
        }
    }
}

jar {
    manifest {
        attributes.putIfAbsent('Built-By', System.properties['user.name'])
        attributes.putIfAbsent('Created-By', "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})")
        attributes.putIfAbsent('Specification-Title', project.name)
        attributes.putIfAbsent('Specification-Version', project.version)
        attributes.putIfAbsent('Specification-Vendor', project.name)
        attributes.putIfAbsent('Implementation-Title', project.name)
        attributes.putIfAbsent('Implementation-Version', project.version)
        attributes.putIfAbsent('Implementation-Vendor', project.name)
    }
    doFirst {
        manifest.attributes.putIfAbsent('Build-Date',commitBuildAndDate.date())
        manifest.attributes.putIfAbsent('Build-Time',commitBuildAndDate.time())
    }
}
