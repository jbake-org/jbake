if ( !project.hasProperty('skipSigning') ) {
    apply plugin: 'signing'

    if (!signing.signatory) {
        logger.warn "No Signatory configured for project $project.name. Skip signing! See https://docs.gradle.org/current/userguide/signing_plugin.html"
        ext.skipSigning = true
    }
    else {
        signing {
            sign configurations.archives
        }
    }

    task signPom(type: Sign) {
        group "publishing"
        description "Sign the projects pom file"

        ext.pom = file("$buildDir/poms/pom-default.xml")
        ext.signedPom = file("$buildDir/poms/${project.name}-${project.version}.pom.asc")
        ext.bintrayDestination = "${project.group.replace(".", "/")}/${project.name}/${project.version}"

        inputs.file pom
        outputs.file signedPom

        doLast {

            def input = pom.newInputStream()
            def output = signedPom.newOutputStream()
            try {
                signatory.sign(input, output)
            }
            catch (Exception e) {
                logger.error e.message
            }
            finally {
                input.close()
                output.close()
            }
        }
    }
}