import java.time.format.DateTimeFormatter

plugins {
    id "org.jbake.convention.java-common"
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm'
}

apply from: "$rootDir/gradle/maven-publishing.gradle"
apply from: "$rootDir/gradle/signing.gradle"

description = "The core library of JBake"

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = "jbake-core"
                licenses { license { name = 'The MIT License (MIT)'; url = 'http://opensource.org/licenses/MIT' } }
            }
        }
    }
}


dependencies {
    api "commons-io:commons-io:$commonsIoVersion"
    api "org.apache.commons:commons-configuration2:$commonsConfigurationVersion"
    implementation "commons-beanutils:commons-beanutils:$commonsBeanutilsVersion"
    compileOnly "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
    implementation("com.googlecode.json-simple:json-simple:$jsonSimpleVersion") {
        exclude group: "junit", module: "junit"
    }
    implementation "com.orientechnologies:orientdb-core:$orientDbVersion"
    compileOnly "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    implementation "org.apache.groovy:groovy:$groovyVersion"
    implementation "org.apache.groovy:groovy-templates:$groovyVersion"
    implementation "org.apache.groovy:groovy-dateutil:$groovyVersion"
    compileOnly "org.freemarker:freemarker:$freemarkerVersion"
    compileOnly "org.thymeleaf:thymeleaf:$thymeleafVersion"
    compileOnly "de.neuland-bfi:jade4j:$jade4jVersion"
    compileOnly "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    compileOnly "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"
    compileOnly "io.pebbletemplates:pebble:$pebbleVersion"
    implementation "org.jsoup:jsoup:$jsoupVersion"
    compileOnly "org.yaml:snakeyaml:$snakeYamlVersion"

    // cli specific dependencies
    compileOnly "org.eclipse.jetty:jetty-server:$jettyServerVersion"
    compileOnly "info.picocli:picocli:$picocli"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Test dependencies need access to optional compile dependencies
    testImplementation "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    testImplementation "info.picocli:picocli:$picocli"
    testImplementation "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    testImplementation "org.freemarker:freemarker:$freemarkerVersion"
    testImplementation "org.thymeleaf:thymeleaf:$thymeleafVersion"
    testImplementation "de.neuland-bfi:jade4j:$jade4jVersion"
    testImplementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    testImplementation "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"
    testImplementation "io.pebbletemplates:pebble:$pebbleVersion"
    testImplementation "org.yaml:snakeyaml:$snakeYamlVersion"
    testImplementation "org.eclipse.jetty:jetty-server:$jettyServerVersion"
}

processResources {
    filesMatching("default.properties") {
        expand jbakeVersion: project.version,
            timestamp: grgit.head().dateTime.format(DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss'['VV']'")),
            gitHash: grgit.head().abbreviatedId
    }
}
repositories {
    mavenCentral()
}
kotlin {
    jvmToolchain(21)
}
