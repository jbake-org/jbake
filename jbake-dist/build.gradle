plugins {
    id "org.jbake.convention.java-common"
    id 'application'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.google.cloud.tools.jib' version '3.4.4'
}

apply from: "$rootDir/gradle/application.gradle"
// JReleaser plugin doesn't support Gradle 9 yet
// apply from: "$rootDir/gradle/release.gradle"
description = "The binary distribution package that bundles JBake cli"

sourceSets {
    smokeTest {
        java {
            srcDir file('src/smoke-test/java')
        }
        resources {
            srcDir file('src/smoke-test/resources')
        }
    }
}

configurations {
    dist

    runtimeOnly.extendsFrom dist
    smokeTestImplementation.extendsFrom testImplementation
    smokeTestCompileOnly.extendsFrom testCompileOnly
    smokeTestRuntimeOnly.extendsFrom testRuntimeOnly
}


dependencies {
    implementation project(':jbake-core')

    // All compileOnly dependencies from jbake-core must be explicitly declared here for runtime CLI dependencies
    implementation "info.picocli:picocli:$picocli"
    implementation "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    implementation "org.eclipse.jetty:jetty-server:$jettyServerVersion"

    // Content processors
    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"
    implementation "org.yaml:snakeyaml:$snakeYamlVersion"
    implementation "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    implementation "org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion"

    // Template engines
    implementation "org.freemarker:freemarker:$freemarkerVersion"
    implementation "org.thymeleaf:thymeleaf:$thymeleafVersion"
    implementation "de.neuland-bfi:jade4j:$jade4jVersion"
    implementation "de.neuland-bfi:pug4j:$pug4jVersion"
    implementation "io.pebbletemplates:pebble:$pebbleVersion"
    // Note: Groovy templates are provided by groovy-templates which is already a transitive dependency

    smokeTestImplementation "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    smokeTestImplementation "org.apache.commons:commons-vfs2:$commonsVfs2Version"
    smokeTestImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    smokeTestImplementation "io.kotest:kotest-assertions-core:$kotestVersion"
    smokeTestImplementation "io.kotest:kotest-framework-engine:$kotestVersion"
    smokeTestImplementation "io.kotest:kotest-property:$kotestVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.register('smokeTest', Test) {
    dependsOn installDist
    group 'Verification'
    description 'Runs the integration tests.'
    setTestClassesDirs sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
}

smokeTest {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

check.dependsOn smokeTest

jar.enabled=false
repositories {
    mavenCentral()
}
kotlin {
    jvmToolchain(21)
}

// Docker image configuration using Jib
jib {
    from {
        image = 'eclipse-temurin:21-jre-alpine'
    }
    to {
        image = 'jbake/jbake'
        tags = [project.version.toString(), 'latest']
    }
    container {
        jvmFlags = ['-XX:MaxDirectMemorySize=512m', '-Djna.nosys=true']
        mainClass = application.mainClass.get()
        workingDirectory = '/mnt/site'
        volumes = ['/mnt/site']
        ports = ['8820']
        environment = [TZ: 'UTC']
        creationTime = 'USE_CURRENT_TIMESTAMP'
        user = '1000:1000'
    }
}
