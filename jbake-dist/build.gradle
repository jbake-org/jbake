apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/sdkman.gradle"
apply from: "$rootDir/gradle/publishing.gradle"

description = "The binary distribution package that bundles JBake cli"

sourceSets {
    smokeTest {
        java {
            srcDir file('src/smoke-test/java')
        }
        resources {
            srcDir file('src/smoke-test/resources')
        }
    }
}

configurations {
    dist

    runtime.extendsFrom dist
}


dependencies {
    compile project(':jbake-core')
    compile("org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion") {
        exclude group: 'org.asciidoctor', module: 'asciidoctorj'
    }

    smokeTestCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    smokeTestCompile configurations.testCompile
    smokeTestCompile sourceSets.test.output
    smokeTestRuntime configurations.testRuntime
}

task smokeTest(type: Test, dependsOn: installDist) {
    group 'Verification'
    description 'Runs the integration tests.'
    setTestClassesDirs sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn smokeTest
