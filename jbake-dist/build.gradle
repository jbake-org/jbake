plugins {
    id "io.sdkman.vendors"  version "2.0.0"
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id "org.jbake.convention.java-common"
    id 'application'
}

apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/sdkman.gradle"
apply from: "$rootDir/gradle/signing.gradle"
apply from: "$rootDir/gradle/github-releases.gradle"

description = "The binary distribution package that bundles JBake cli"

sourceSets {
    smokeTest {
        java {
            srcDir file('src/smoke-test/java')
        }
        resources {
            srcDir file('src/smoke-test/resources')
        }
    }
}

configurations {
    dist

    runtimeOnly.extendsFrom dist
    smokeTestImplementation.extendsFrom testImplementation
    smokeTestCompileOnly.extendsFrom testCompileOnly
    smokeTestRuntimeOnly.extendsFrom testRuntimeOnly
}


dependencies {
    implementation project(':jbake-core')
    implementation("org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion") {
        exclude group: 'org.asciidoctor', module: 'asciidoctorj'
    }

    smokeTestImplementation "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    smokeTestImplementation "org.apache.commons:commons-vfs2:$commonsVfs2Version"
}

task smokeTest(type: Test, dependsOn: installDist) {
    group 'Verification'
    description 'Runs the integration tests.'
    setTestClassesDirs sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
    shouldRunAfter test
}

smokeTest {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task buildChecksum(dependsOn: distZip, group: "distribution") {
    description "creates a sha256 checksum file for the distribution"
    doLast {
        distZip.outputs.files.files.each {
            ant.checksum(file: it, fileext: ".sha256", algorithm: "SHA-256", pattern: "{0} {1}")
        }
    }
}

check.dependsOn smokeTest

distZip.finalizedBy buildChecksum

jar.enabled=false
