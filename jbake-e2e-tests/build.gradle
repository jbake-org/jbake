//file:noinspection GroovyAssignabilityCheck

plugins {
    id "org.jbake.convention.java-common"
    id 'org.jetbrains.kotlin.jvm'
}

description = "End-to-end tests for JBake using TestContainers"

// This module is only for testing, skip deployment
tasks.withType(PublishToMavenRepository).configureEach {
    enabled = false
}

sourceSets {
    test {
        kotlin {
            srcDir 'src/test/kotlin'
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // TestContainers 2.0.2 - artifact renamed from junit-jupiter to testcontainers-junit-jupiter
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:testcontainers-junit-jupiter:$testcontainersVersion"
    testImplementation "com.github.docker-java:docker-java-core:$dockerJavaVersion"
    testImplementation "com.github.docker-java:docker-java-api:$dockerJavaVersion"
    testImplementation "com.github.docker-java:docker-java-transport-zerodep:$dockerJavaVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-annotations:2.20" // Not $jacksonVersion... wrong version published
    testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"

    // Testing frameworks
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"

    // Utilities
    testImplementation "org.assertj:assertj-core:$assertjCoreVersion"
    testImplementation "commons-io:commons-io:$commonsIoVersion"
    testImplementation "org.jsoup:jsoup:$jsoupVersion"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }

    // Pass test data directory location
    systemProperty "jbake.test.data.dir", "${projectDir}/../test-data/fixture"

    // Docker API version for compatibility with modern Docker (1.44+)
    systemProperty "DOCKER_API_VERSION", "1.44"

    // TestContainers configuration
    systemProperty "testcontainers.reuse.enable", "false"
    environment "TESTCONTAINERS_RYUK_DISABLED", "true"
    environment "DOCKER_API_VERSION", "1.44"

    // Workaround for nested mount issue in TC 2.x - see https://github.com/testcontainers/testcontainers-java/issues/11212
    environment "TESTCONTAINERS_DOCKER_CLIENT_STRATEGY", "org.testcontainers.dockerclient.UnixSocketClientProviderStrategy"

    // Allow DOCKER_HOST to be passed through from environment (for rootless Docker)
    if (System.getenv("DOCKER_HOST")) {
        environment "DOCKER_HOST", System.getenv("DOCKER_HOST")
    }

    // Increase timeout for container operations
    timeout = Duration.ofMinutes(10)
}

kotlin {
    jvmToolchain(javaVersion.toInteger())
}

repositories {
    mavenCentral()
}

