plugins {
    id "org.jbake.convention.java-common"
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

group = "org.jbake"

apply from: "$rootDir/gradle/signing.gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = "jbake-maven-plugin"
                description = "Maven plugin for JBake"
                url = project.website
                packaging = 'maven-plugin'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url  = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'jonbullock'
                        name = 'Jonathan Bullock'
                        email = 'jonbullock@gmail.com'
                    }
                }

                scm {
                    connection = "scm:git:${project.vcs}.git"
                    developerConnection = "scm:git:${project.vcs}.git"
                    url = project.vcs
                }
            }
        }
    }
}

dependencies {
    implementation project(":jbake-core")

    compileOnly "org.apache.maven:maven-core:$mavenVersion"
    compileOnly "org.apache.maven.plugin-tools:maven-plugin-annotations:$mavenAnnotationsVersion"

    implementation "com.sparkjava:spark-core:$sparkVersion"

    // Include all optional dependencies by default
    implementation "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    implementation "org.apache.groovy:groovy:$groovyVersion"
    implementation "org.apache.groovy:groovy-templates:$groovyVersion"
    implementation "org.apache.groovy:groovy-dateutil:$groovyVersion"
    implementation "org.freemarker:freemarker:$freemarkerVersion"
    implementation "org.thymeleaf:thymeleaf:$thymeleafVersion"
    implementation "de.neuland-bfi:pug4j:$pug4jVersion"
        implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion" // To avoid ERROR void sun.misc.Unsafe.ensureClassInitialized(java.lang.Class)
    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-profile-pegdown:$flexmarkVersion"
    implementation "io.pebbletemplates:pebble:$pebbleVersion"
    implementation "org.yaml:snakeyaml:$snakeYamlVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}
repositories {
    mavenCentral()
}
kotlin {
    jvmToolchain(javaVersion.toInteger())
}
